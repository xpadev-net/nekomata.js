""# このスクリプトはご自由に使っていただいて結構です。
""# コピーときは、矢印の範囲内のみを選択すると、背景とテレビちゃんが描画されません。
""#
""# 作成にあたり参考にさせてもらった動画
""# ・モノクロスロード(nm13390455) By ゆうきさん
""# ・ニコスで懐かしいコメントを再現してみた(nm10645548) By ボノボさん
""#------------------ 　↓↓↓　　コピー範囲　ここから　↓↓↓　 ------------------
isWide=true
def_kari("mk", $2.times(\timer(timer:(@0+1)*$1, $3[(@0+1)*$1])))
rnd=[]; 200.times(\rnd[@0]=[rand(), rand(), rand()])
postDisabled=true; timer(timer:0.3, postDisabled=false)
interval=0.08; y0=200; Lxmin=50; Lxmax=250; xmin=-160; xmax=160; ymin=-200; ymax=-70; tmin=0.65; tmax=0.85; smin=22; smax=64; strmax=16
fontcolor=[0xFF8282, 0xFFC082, 0xFFFF82, 0xC0FF82, 0x82FF82, 0x82C0FF, 0x82FFFF, 0x8282FF, 0xFF82FF, 0x82FFC0, 0xC082FF, 0xFF82C0]
def_kari('hopping', $1.text=chat.message.slice(0, strmax); r1:=rnd[count%200][0]; r2:=rnd[count%200][1]; r3:=rnd[count%200][2]; ytop:=ymin+r1%(ymax-ymin); ttop:=tmin+(r2/100)%(tmax-tmin); v0:=2*(ytop-y0)/ttop; acc:=-v0/ttop; xtop:=xmin+r3%(xmax-xmin); vx:=(r1<>r2)*(Lxmin+(r2+r3)%(Lxmax-Lxmin))/ttop; $1.x=xtop-vx*ttop; $1.y=y0; $1.size=smin+(r3+r1)%(smax-smin); $1.color=fontcolor[r1%fontcolor.size]; dx:=vx*interval; mk(interval, 2*ttop/interval+1, \($1.x+=dx; $1.y=acc*@0.pow(2)/2+v0*@0+y0)))
msg=[]; 13.times(\msg[@0]=dt(z:3))
repl=replace(src:"", dest:" ", enabled:true, fill:true)
count=-1; Lasthop=0; commentTrigger((((chat.vpos-Lasthop)>0.17)||chat.isYourPost).alt(Lasthop=chat.vpos; hopping(msg[(count++)%msg.size])), timer:53.4)
seek(vpos:11)
""#------------------ 　↑↑↑　　コピー範囲　ここまで　↑↑↑　 ------------------
""#
""#--------------- 　以下は、背景とテレビちゃんに関するスクリプトです -------------
"--------  【レイヤー作成】 --------"
layer=[]
layer[0]=drawShape(z:0,x:-20,y:0,width:700,height:400,color:0xFEFDDD,shape:'rect',pos:'hidari ue')
layer[1]=drawShape(z:1,y:60,width:0,height:0,color:0xD6D5BF)
layer[2]=drawShape(z:2,y:60,width:0,height:0,color:0xFEFDDD)
layer[3]=drawShape(z:4,x:255,y:341,width:21,height:35,color:0x2A2820,pos:'hidari ue')
layer[4]=drawShape(z:5,x:262,y:348,width:7,height:21,color:0xFEFEFE,pos:'hidari ue')
layer[5]=drawShape(z:6,x:355,y:336,width:25,height:39,color:0x2A2820,pos:'hidari ue')
layer[6]=drawShape(z:7,x:362,y:343,width:11,height:25,color:0xFEFEFE,pos:'hidari ue')
layer[7]=drawText(z:8,x:1,y:58,size:94,color:0x2A2820,text:'＼')
layer[8]=drawText(z:9,x:-2,y:57,size:94,color:0x2A2820,text:'／')
layer[18]=drawShape(z:10,x:0,y:245,width:672,height:114,color:0xFEFDDD,shape:'rect',pos:'hidari ue')
layer[9]=drawShape(z:11,x:240,y:245,width:160,height:114,color:0x2A2820,shape:'rect',pos:'hidari ue')
layer[10]=drawShape(z:12,x:246,y:251,width:148,height:102,color:0xFEFEFE,shape:'rect',pos:'hidari ue')
layer[11]=drawShape(z:13,x:253,y:258,width:134,height:88,color:0x2A2820,shape:'rect',pos:'hidari ue')
layer[12]=drawShape(z:14,x:258,y:263,width:124,height:78,color:0xFEFEFE,shape:'rect',pos:'hidari ue')
layer[13]=drawShape(z:15,x:-45,y:104,width:19,height:17,color:0x2A2820)
layer[14]=drawShape(z:16,x:43,y:94,width:19,height:17,color:0x2A2820)
layer[15]=drawText(z:17,x:0,y:131,size:28,color:0x2A2820,text:'▲')
layer[16]=drawShape(z:18,x:-47,y:126,width:0,height:9,color:0xFFC793)
layer[17]=drawShape(z:19,x:47,y:126,width:0,height:9,color:0xFFC793)
"--------【アニメーションを設定】---------"
def_kari('do',i:=$1-1;while_kari(++i<=$2,$3[i]))
bg_state=[[85,81],[213,208],[306,301],[371,367],[0,0]]
tv_state=[18,[45,39],[-15,15],[-15,-7],[[-30,-26],[-15,-13]],5,[5,-3,-2]]
tv_initial=\(layer[@0].y+=tv_state[0])
tv_blink=\(@(j);timer(timer:0.1*j,do(13,14,\(layer[@0].height+=tv_state[2][j]))))
tv_up=\(@(i_out);do(7,18,\(@(i_in);timer(timer:0.1*i_out,layer[i_in].y+=tv_state[3][i_out]))))
tv_l_antena=\(timer(timer:0.1*@0,layer[7].x+=tv_state[4][@0][0];layer[7].y+=tv_state[4][@0][1]))
tv_r_antena=\(timer(timer:0.1*@0,layer[8].x-=tv_state[4][@0][0];layer[8].y+=tv_state[4][@0][1]))
tv_set=\(layer[@0].y+=tv_state[5])
tv_shake1=\(@(j);timer(timer:0.1*j,do(7,15,\(layer[@0].y+=tv_state[6][j]))))
tv_shake2=\(@(j);timer(timer:0.1*j,do(7,15,\(layer[@0].y+=tv_state[6][j];layer[@0].x-=5));do(1,6,\(layer[@0].x-=5))))
tv_shake3=\(@(j);timer(timer:0.1*j,do(7,15,\(layer[@0].y+=tv_state[6][j];layer[@0].x+=5));do(1,6,\(layer[@0].x+=5))))
tv_shake4=\(@(j);timer(timer:0.1*j,do(7,17,\(layer[@0].y+=tv_state[6][j]))))
def_kari("tv_jump", do(7,17,\layer[@0].y-=5); do(0, 2*$1, \(@(j); timer(timer:(j+1)*interval, do(3, 17, \(layer[@0].y+=100*(j-$1)*interval.pow(2); layer[@0].x+=$2))))); timer(timer:(2*$1+2)*interval, do(7, 17, \layer[@0].y+=5)))
bg_extend=\(timer(timer:0.06*@0,layer[1].width=layer[1].height=bg_state[@0][0];layer[2].width=layer[2].height=bg_state[@0][1]))
"--------【アニメーションを実行】---------"
do(7,18,tv_initial);layer[7].x+=tv_state[1][0];layer[7].y+=tv_state[1][1];layer[8].x-=tv_state[1][0];layer[8].y+=tv_state[1][1]
do(0,tv_state[2].size-1,tv_blink)
do(0,tv_state[2].size-1,tv_blink)
do(0,tv_state[2].size-1,tv_blink)
do(0,tv_state[3].size-1,tv_up)
do(0,tv_state[4].size-1,tv_l_antena)
do(0,tv_state[4].size-1,tv_r_antena)
do(7,18,tv_set);layer[18].width=0
do(0,31,\(timer(timer:60/142*@0,do(0,2,tv_shake1);do(0,4,bg_extend))))
do(0,7,\(timer(timer:60/142*@0,do(0,2,tv_shake2);do(0,4,bg_extend))))
do(0,15,\(timer(timer:60/142*@0,do(0,2,tv_shake3);do(0,4,bg_extend))))
do(0,7,\(timer(timer:60/142*@0,do(0,2,tv_shake2);do(0,4,bg_extend))))
layer[16].width=layer[17].width=23; do(0,31,\(timer(timer:60/142*@0,do(0,2,tv_shake4);do(0,4,bg_extend))))
do(7,18,\layer[@0].y+=5)
tv_jump(8,2)
tv_jump(16,-3)
tv_jump(18,4)
tv_jump(28,-10)